// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#include "/Engine/Public/Platform.ush"

//----------------------------------------------------------------------------------

uint IndexSize; // bone index size: 1 - 8bit or 2 - 16bit
uint SectionVertexBaseIndex;
uint NumTotalVertices;
uint WeightStride;
uint BonesOffset;

Buffer<uint> WeightLookup;
Buffer<uint> VertexWeights;
Buffer<float> RestPositions;

// Current Position
Buffer<float4> BoneMatrices;
RWBuffer<float> DeformedPositions;

// Previous Position
#if PERMUTATION_PREV
Buffer<float4> PrevBoneMatrices;
RWBuffer<float> PrevDeformedPositions;
#endif

//----------------------------------------------------------------------------------

#define	GPUSKIN_VB_OFFSET_INFLUENCEBONES	0
#define	GPUSKIN_VB_OFFSET_INFLUENCEWEIGHTS	((1 + GPUSKIN_USE_EXTRA_INFLUENCES) * (1 + GPUSKIN_BONE_INDEX_UINT16))

//----------------------------------------------------------------------------------

#define FBlendedMatrix float3x4

struct FUnpackedWeight
{
#if !GPUSKIN_UNLIMITED_BONE_INFLUENCE
	int4	BlendIndices;
#if GPUSKIN_USE_EXTRA_INFLUENCES
	int4	ExtraIndices;
#endif
	float4	BlendWeights;
#if GPUSKIN_USE_EXTRA_INFLUENCES
	float4	ExtraWeights;
#endif
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE
};

//----------------------------------------------------------------------------------

FBlendedMatrix BlendMatrix(FUnpackedWeight UnpackedWeight, uint VertexIndex, Buffer<float4> InBoneMatrices)
{
	const uint MatrixOffset = BonesOffset;
#if GPUSKIN_UNLIMITED_BONE_INFLUENCE
	uint BlendOffsetCount = WeightLookup[VertexIndex];
	int NumBoneInfluences = BlendOffsetCount & 0xff;
	int StreamOffset = BlendOffsetCount >> 8;
	int WeightsOffset = StreamOffset + (IndexSize * NumBoneInfluences);

	FBlendedMatrix OutBoneMatrix = FBlendedMatrix(float4(0,0,0,0), float4(0,0,0,0), float4(0,0,0,0));
	for (int InfluenceIdx = 0; InfluenceIdx < NumBoneInfluences; InfluenceIdx++)
	{
		int BoneIndexOffset = StreamOffset + (IndexSize * InfluenceIdx);
		int BoneIndex = VertexWeights[BoneIndexOffset];
		BoneIndex = (IndexSize > 1) ? VertexWeights[BoneIndexOffset + 1] << 8 | BoneIndex : BoneIndex;
		
		const uint IndexOffset = (MatrixOffset + BoneIndex) * 3;
		//@todo-lh: Workaround to fix issue in SPIRVEmitter of DXC; this block must be inside the if branch
		float BoneWeight = float(VertexWeights[WeightsOffset + InfluenceIdx]) / 255.0f;
		OutBoneMatrix += BoneWeight * FBlendedMatrix(InBoneMatrices[IndexOffset], InBoneMatrices[IndexOffset+1], InBoneMatrices[IndexOffset+2]);
	}
	
#else // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	uint IndexOffsetX = (MatrixOffset + UnpackedWeight.BlendIndices.x) * 3;
	uint IndexOffsetY = (MatrixOffset + UnpackedWeight.BlendIndices.y) * 3;
	uint IndexOffsetZ = (MatrixOffset + UnpackedWeight.BlendIndices.z) * 3;
	uint IndexOffsetW = (MatrixOffset + UnpackedWeight.BlendIndices.w) * 3;

	FBlendedMatrix OutBoneMatrix = UnpackedWeight.BlendWeights.x * FBlendedMatrix(InBoneMatrices[IndexOffsetX], InBoneMatrices[IndexOffsetX+1], InBoneMatrices[IndexOffsetX+2]);
	OutBoneMatrix += UnpackedWeight.BlendWeights.y * FBlendedMatrix(InBoneMatrices[IndexOffsetY], InBoneMatrices[IndexOffsetY+1], InBoneMatrices[IndexOffsetY+2]);
	OutBoneMatrix += UnpackedWeight.BlendWeights.z * FBlendedMatrix(InBoneMatrices[IndexOffsetZ], InBoneMatrices[IndexOffsetZ+1], InBoneMatrices[IndexOffsetZ+2]);
	OutBoneMatrix += UnpackedWeight.BlendWeights.w * FBlendedMatrix(InBoneMatrices[IndexOffsetW], InBoneMatrices[IndexOffsetW+1], InBoneMatrices[IndexOffsetW+2]);
  #if GPUSKIN_USE_EXTRA_INFLUENCES
	IndexOffsetX = (MatrixOffset + UnpackedWeight.ExtraIndices.x) * 3;
	IndexOffsetY = (MatrixOffset + UnpackedWeight.ExtraIndices.y) * 3;
	IndexOffsetZ = (MatrixOffset + UnpackedWeight.ExtraIndices.z) * 3;
	IndexOffsetW = (MatrixOffset + UnpackedWeight.ExtraIndices.w) * 3;

	OutBoneMatrix += UnpackedWeight.ExtraWeights.x * FBlendedMatrix(BoneMatrices[IndexOffsetX], BoneMatrices[IndexOffsetX+1], BoneMatrices[IndexOffsetX+2]);
	OutBoneMatrix += UnpackedWeight.ExtraWeights.y * FBlendedMatrix(BoneMatrices[IndexOffsetY], BoneMatrices[IndexOffsetY+1], BoneMatrices[IndexOffsetY+2]);
	OutBoneMatrix += UnpackedWeight.ExtraWeights.z * FBlendedMatrix(BoneMatrices[IndexOffsetZ], BoneMatrices[IndexOffsetZ+1], BoneMatrices[IndexOffsetZ+2]);
	OutBoneMatrix += UnpackedWeight.ExtraWeights.w * FBlendedMatrix(BoneMatrices[IndexOffsetW], BoneMatrices[IndexOffsetW+1], BoneMatrices[IndexOffsetW+2]);
  #endif
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE
	return OutBoneMatrix;
}

//----------------------------------------------------------------------------------

FUnpackedWeight UnpackWeight(uint WeightOffset)
{
	FUnpackedWeight Unpacked;

#if !GPUSKIN_UNLIMITED_BONE_INFLUENCE
	uint BlendIndices = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEBONES];
#if GPUSKIN_BONE_INDEX_UINT16
	Unpacked.BlendIndices.x = BlendIndices & 0xffff;
	Unpacked.BlendIndices.y = BlendIndices >> 16 & 0xffff;
	BlendIndices = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEBONES + 1];
	Unpacked.BlendIndices.z = BlendIndices & 0xffff;
	Unpacked.BlendIndices.w = BlendIndices >> 16 & 0xffff;
#if GPUSKIN_USE_EXTRA_INFLUENCES
	BlendIndices = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEBONES + 2];
	Unpacked.ExtraIndices.x = BlendIndices & 0xffff;
	Unpacked.ExtraIndices.y = BlendIndices >> 16 & 0xffff;
	BlendIndices = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEBONES + 3];
	Unpacked.ExtraIndices.z = BlendIndices & 0xffff;
	Unpacked.ExtraIndices.w = BlendIndices >> 16 & 0xffff;
#endif
#else // GPUSKIN_BONE_INDEX_UINT16
	Unpacked.BlendIndices.x = BlendIndices & 0xff;
	Unpacked.BlendIndices.y = BlendIndices >> 8 & 0xff;
	Unpacked.BlendIndices.z = BlendIndices >> 16 & 0xff;
	Unpacked.BlendIndices.w = BlendIndices >> 24 & 0xff;
#if GPUSKIN_USE_EXTRA_INFLUENCES
	BlendIndices = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEBONES + 1];
	Unpacked.ExtraIndices.x = BlendIndices & 0xff;
	Unpacked.ExtraIndices.y = BlendIndices >> 8 & 0xff;
	Unpacked.ExtraIndices.z = BlendIndices >> 16 & 0xff;
	Unpacked.ExtraIndices.w = BlendIndices >> 24 & 0xff;
#endif
#endif // GPUSKIN_BONE_INDEX_UINT16
	uint BlendWeights = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEWEIGHTS];
	Unpacked.BlendWeights.x = float(BlendWeights & 0xff) / 255.0f;
	Unpacked.BlendWeights.y = float(BlendWeights >> 8 & 0xff) / 255.0f;
	Unpacked.BlendWeights.z = float(BlendWeights >> 16 & 0xff) / 255.0f;
	Unpacked.BlendWeights.w = float(BlendWeights >> 24 & 0xff) / 255.0f;
#if GPUSKIN_USE_EXTRA_INFLUENCES
	BlendWeights = VertexWeights[WeightOffset + GPUSKIN_VB_OFFSET_INFLUENCEWEIGHTS + 1];
	Unpacked.ExtraWeights.x = float(BlendWeights & 0xff) / 255.0f;
	Unpacked.ExtraWeights.y = float(BlendWeights >> 8 & 0xff) / 255.0f;
	Unpacked.ExtraWeights.z = float(BlendWeights >> 16 & 0xff) / 255.0f;
	Unpacked.ExtraWeights.w = float(BlendWeights >> 24 & 0xff) / 255.0f;
#endif
#endif // GPUSKIN_UNLIMITED_BONE_INFLUENCE

	return Unpacked;
}

//----------------------------------------------------------------------------------

[numthreads(64,1,1)]
void UpdateSkinPositionCS(uint3 GroupID : SV_GroupID,
 uint3 DispatchThreadID : SV_DispatchThreadID,
 uint3 GroupThreadID : SV_GroupThreadID)
{
	const uint VertexIndex = DispatchThreadID.x + SectionVertexBaseIndex;
	if (VertexIndex < NumTotalVertices)
	{
		const uint PositionOffset = VertexIndex * 3;
		const uint WeightOffset = VertexIndex * WeightStride / 4;

		const FUnpackedWeight UnpackedWeight = UnpackWeight(WeightOffset);

		const float4 InputPosition = float4(
			RestPositions[PositionOffset],
			RestPositions[PositionOffset + 1],
			RestPositions[PositionOffset + 2],
			1.0);

		// Current position
		{
			const FBlendedMatrix BlendedMatrix = BlendMatrix(UnpackedWeight, VertexIndex, BoneMatrices);
			const float3 OutputPosition =  mul(BlendedMatrix, InputPosition);
			DeformedPositions[PositionOffset    ] = OutputPosition.x;
			DeformedPositions[PositionOffset + 1] = OutputPosition.y;
			DeformedPositions[PositionOffset + 2] = OutputPosition.z;
		}

		// Previous position
	#if PERMUTATION_PREV
		{
			const FBlendedMatrix PrevBlendedMatrix = BlendMatrix(UnpackedWeight, VertexIndex, PrevBoneMatrices);
			const float3 OutputPosition = mul(PrevBlendedMatrix, InputPosition);
			PrevDeformedPositions[PositionOffset    ] = OutputPosition.x;
			PrevDeformedPositions[PositionOffset + 1] = OutputPosition.y;
			PrevDeformedPositions[PositionOffset + 2] = OutputPosition.z;
		}
	#endif
	}
}
