// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#include "NaniteRasterizationCommon.ush"
#include "NaniteTessellation.ush"

#if NANITE_TESSELLATION

void RasterizeDicedTri(
	FRasterTri		Tri,
	FRaster			Raster,
	FMaterialShader	Shader,
	uint			PixelValue,
	uint2			VisualizeValues )
{
#if VIRTUAL_TEXTURE_TARGET && NANITE_LATE_VSM_PAGE_TRANSLATION
	if (!Raster.bSinglePage)
	{
		TNaniteWritePixel< FMaterialShader, FCachedPageTable > NaniteWritePixel = { Raster, Shader, PixelValue, VisualizeValues };
		RasterizeTri_Rect( Tri, NaniteWritePixel );
	}
	else
#elif VIRTUAL_TEXTURE_TARGET
	if (!Raster.bSinglePage)
	{
		TNaniteWritePixel< FMaterialShader, FFetchPageTable > NaniteWritePixel =
		{
			Raster,
			Shader,
			PixelValue,
			VisualizeValues,
			Shader.NaniteView.TargetMipLevel,
			CalcPageTableLevelOffset( Shader.NaniteView.TargetLayerIndex, Shader.NaniteView.TargetMipLevel ).LevelOffset
		};
		
		RasterizeTri_Rect( Tri, NaniteWritePixel );
	}
	else
#endif
	{
		TNaniteWritePixel< FMaterialShader > NaniteWritePixel = { Raster, Shader, PixelValue, VisualizeValues };
		RasterizeTri_Rect( Tri, NaniteWritePixel );
	}
}

struct FDiceTask
{
	FRaster			Raster;
	FMaterialShader	Shader;
	uint			PixelValue;
	uint2			VisualizeValues;
	bool			bReverseWinding;

	FNaniteTransformedVert	Vert;
	FTessellatedPatch		TessellatedPatch;
	
	uint4	Encoded;
	uint	PatchData;

	// Vertex cache
	//uint	FirstVert;
	//uint&	NumCached;

	void Init( float3 TessFactors, uint3 VertIndexes, uint TriIndex )
	{
		TessellatedPatch.Init( TessFactors, VertIndexes );
	
		PatchData  = VertIndexes.x << 0;
		PatchData |= VertIndexes.y << 8;
		PatchData |= VertIndexes.z << 16;
		PatchData |= TriIndex << 24;
	}

	FDiceTask CreateChild( uint ParentLaneIndex )
	{
		// "this" is broken: https://github.com/microsoft/DirectXShaderCompiler/issues/4914
		FDiceTask ChildTask;// = this;
		ChildTask.Raster			= Raster;
		ChildTask.Shader			= Shader;
		ChildTask.PixelValue		= PixelValue;
		ChildTask.VisualizeValues	= VisualizeValues;
		ChildTask.bReverseWinding	= bReverseWinding;

		ChildTask.TessellatedPatch	= WaveReadLaneAt( TessellatedPatch, ParentLaneIndex );
		ChildTask.Encoded			= WaveReadLaneAt( Encoded, ParentLaneIndex );
		ChildTask.PatchData			= WaveReadLaneAt( PatchData, ParentLaneIndex );
		//ChildTask.FirstVert		= WaveReadLaneAt( FirstVert, ParentLaneIndex );

		uint3 PatchVertIndexes;
		PatchVertIndexes.x = ( ChildTask.PatchData >>  0 ) & 0xff;
		PatchVertIndexes.y = ( ChildTask.PatchData >>  8 ) & 0xff;
		PatchVertIndexes.z = ( ChildTask.PatchData >> 16 ) & 0xff;

		FNaniteTransformedTri TransformedTri = MakeTransformedNaniteTriangle( Vert, PatchVertIndexes );
		ChildTask.Shader.TransformedTri = TransformedTri;	// TODO mutable. This is weird

		return ChildTask;
	}

	void RunChild( uint LocalItemIndex );
};

//groupshared float3			VertexCache[ THREADGROUP_SIZE ];
//#define VertCache(x)		VertexCache[ QueueOffset + ( (x) & ( LaneCount - 1 ) ) ]

void FDiceTask::RunChild( uint LocalItemIndex )
{
	uint PatchIndex = PatchData >> 24;

	uint3 VertIndexes = TessellatedPatch.GetIndexes( LocalItemIndex );

	if( bReverseWinding )
		VertIndexes.yz = VertIndexes.zy;
	
	float4 Verts[3];
	
#if 0
	// Grab what's there for this triangle before updating cache. Otherwise cache would need to be double size.
	bool3 VertRead = false;

	UNROLL
	for( uint k = 0; k < 3; k++ )
	{
		if( FirstVert + VertIndexes[k] < NumCached )
		{
			Verts[k] = VertCache( FirstVert + VertIndexes[k] );
			VertRead[k] = true;
		}
	}

	GroupMemoryBarrier();
			
	bool bNewVertex = PackedIndexes & (1 << 30);
	if( bNewVertex )
	{
		uint MaxVertIndex = max3( VertIndexes.x, VertIndexes.y, VertIndexes.z );

		float3 UVW = TessellatedPatch.GetVert( MaxVertIndex );
		UVW = TransformBarycentrics( UVW );

		FBarycentrics Barycentrics;
		Barycentrics.UVW 	= UVW;
		Barycentrics.UVW_dx	= 0;
		Barycentrics.UVW_dy	= 0;

		VertCache( FirstVert + MaxVertIndex ) = CalculateSubpixelCoordinates( Raster, Shader.EvaluateDomain( Barycentrics ) );
	}
			
	GroupMemoryBarrier();

	NumCached += WaveActiveCountBits( bNewVertex );		//FIXME this increments LocalTask.NumCached which goes no where. Need persistent scalar. Need references!

	UNROLL
	for( uint k = 0; k < 3; k++ )
	{
		if( !VertRead[k] )
			Verts[k] = VertCache( FirstVert + VertIndexes[k] );
	}
#else
	float3 TessFactors = TessellatedPatch.GetTessFactors();

	UNROLL
	for( uint i = 0; i < 3; i++ )
	{
		FBarycentrics Barycentrics;
		Barycentrics.UVW	= TessellatedPatch.GetVert( VertIndexes[i] );
		Barycentrics.UVW_dx	= 0;	// float3( -1, 1, 0 ) / TessFactors.x;
		Barycentrics.UVW_dy	= 0;	// float3( 0, -1, 1 ) / TessFactors.y;

		Verts[i] = CalculateSubpixelCoordinates( Raster, Shader.EvaluateDomain( Barycentrics ) );
	}
#endif

	FRasterTri Tri = SetupTriangle< NANITE_SUBPIXEL_SAMPLES, !NANITE_TWO_SIDED >( Raster.ScissorRect, Verts );

	if( Tri.bIsValid )
	{
	#if VISUALIZE
		VisualizeValues.x = LocalItemIndex;
	#endif

		RasterizeDicedTri(
			Tri,
			Raster,
			Shader,
			PixelValue | PatchIndex,
			VisualizeValues );
	}
}

struct FClusterSplitTask
{
	FTessellatedPatch	TessellatedPatch;

	uint4	Encoded;

	void				Init( float3 TessFactors, uint VisibleClusterIndex, uint TriIndex );

	FClusterSplitTask	CreateChild( uint ParentLaneIndex );
	void				RunChild( uint LocalItemIndex );
};

void FClusterSplitTask::Init( float3 TessFactors, uint VisibleClusterIndex, uint TriIndex )
{
	Encoded.x = ( VisibleClusterIndex << 7 ) | TriIndex;
	Encoded.y = BarycentricMax;
	Encoded.z = BarycentricMax << 16;
	Encoded.w = 0;

	TessellatedPatch.Init( TessFactors, Encoded.yzw );
}

FClusterSplitTask FClusterSplitTask::CreateChild( uint ParentLaneIndex )
{
	FClusterSplitTask ChildTask;
	ChildTask.TessellatedPatch	= WaveReadLaneAt( TessellatedPatch, ParentLaneIndex );
	ChildTask.Encoded			= WaveReadLaneAt( Encoded, ParentLaneIndex );
	return ChildTask;
}

void FClusterSplitTask::RunChild( uint LocalItemIndex )
{
#if 0
	Encoded.yzw = TessellatedPatch.GetTriangleEncoded( LocalItemIndex );
#else
	FSplitPatch Patch;
	Patch.Decode( Encoded );
		
	uint3 VertIndexes = TessellatedPatch.GetIndexes( LocalItemIndex );

	for( int i = 0; i < 3; i++ )
	{
		float3 Barycentrics = TessellatedPatch.GetVert( VertIndexes[i] );

		Barycentrics = 
			Patch.Barycentrics[0] * Barycentrics.x +
			Patch.Barycentrics[1] * Barycentrics.y +
			Patch.Barycentrics[2] * Barycentrics.z;
		
		Encoded[ i + 1 ] = EncodeBarycentrics( Barycentrics );
	}
#endif

	uint WriteOffset = SplitWorkQueue.Add();
	if( WriteOffset < SplitWorkQueue.Size )
	{
		checkSlow(
			Encoded.x != ~0u &&
			Encoded.y != ~0u &&
			Encoded.z != ~0u &&
			Encoded.w != ~0u );

		SplitWorkQueue.DataBuffer.Store4( WriteOffset * 16, Encoded );
	}
}

#endif