// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
	RayTracingLightCullingCommon.ush: Common functions for culling lights.
===============================================================================================*/

#pragma once

// *************** LIGHT CULLING *********************************
// Use a 2D grid built along the longest axes of the bounding boxes of all finite light sources.

struct FCulledLightList
{
	uint NumLights;
	uint LightGridOffset;

	static FCulledLightList Create(float3 TranslatedWorldPos)
	{
		FCulledLightList Result = (FCulledLightList) 0;
		Result.NumLights = RaytracingLightGridData.SceneInfiniteLightCount;
		Result.LightGridOffset = ~0u;
		
		const float3 SceneMin = RaytracingLightGridData.SceneLightsTranslatedBoundMin;
		const float3 SceneMax = RaytracingLightGridData.SceneLightsTranslatedBoundMax;
		if (all(SceneMin <= TranslatedWorldPos) &&
			all(TranslatedWorldPos <= SceneMax))
		{
			float3 P = TranslatedWorldPos - SceneMin;
			float3 D = SceneMax - SceneMin;
			int2 UV = 0;
			switch (RaytracingLightGridData.LightGridAxis)
			{
				case 0: UV = int2(floor(RaytracingLightGridData.LightGridResolution * P.yz / D.yz)); break;
				case 1: UV = int2(floor(RaytracingLightGridData.LightGridResolution * P.xz / D.xz)); break;
				case 2: UV = int2(floor(RaytracingLightGridData.LightGridResolution * P.xy / D.xy)); break;
			}
			uint LightGridN = RaytracingLightGridData.LightGrid.Load(int3(UV, 0));
			Result.LightGridOffset = RaytracingLightGridData.LightGridMaxCount * (UV.x + UV.y * RaytracingLightGridData.LightGridResolution);
			Result.NumLights += LightGridN;
		}
		return Result;
	}

	uint GetNumLights()
	{
		return NumLights;
	}

	uint GetLightIndex(int LightNum, out uint Valid)
	{
		if (LightNum >= NumLights)
		{
			Valid = 0;
			return 0;
		}
		Valid = 1;
		if (LightNum >= RaytracingLightGridData.SceneInfiniteLightCount)
		{
			return RaytracingLightGridData.LightGridData[LightGridOffset + LightNum - RaytracingLightGridData.SceneInfiniteLightCount];
		}
		return LightNum;
	}

	float3 Visualize(int VisualizeMode)
	{
		const float3 OutsideColor = 0.18;
		const float3 EmptyColor = 0.36;

		if (LightGridOffset != ~0u)
		{
			switch (VisualizeMode)
			{
				case 2:
				{
					// color by unique light list
					uint H = 0;
					for (int Index = 0; Index < NumLights; Index++)
					{
						uint Valid = 0;
						uint LightId = GetLightIndex(Index, Valid);
						if (Valid)
						{
							H += LightId;
							H ^= H >> 16;
							H *= 0xa812d533;
							H ^= H >> 15;
							H *= 0xb278e4ad;
							H ^= H >> 17;
						}
					}
					return 0.5 + 0.3 * cos(2 * PI * ((H & 0xFFFFFF) * 5.96046447754e-08 + float3(0.0, 0.333, 0.6666)));
				}
				default: 
				{
					// default mode - color by light count
					uint N = NumLights - RaytracingLightGridData.SceneInfiniteLightCount;
					if (N < 1)
					{
						return EmptyColor;
					}
					float Max = RaytracingLightGridData.LightGridMaxCount;
					float t = saturate(float(N) / Max);
					return t * saturate(2.0 - abs(float3(4, 2, 0) - 4.0 * t));
				}
			}
		}
		// outside light grid bounds
		return OutsideColor;
	}
};

