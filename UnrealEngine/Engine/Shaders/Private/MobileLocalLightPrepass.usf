// Copyright Epic Games, Inc. All Rights Reserved.

#define SUPPORT_CONTACT_SHADOWS 0

#include "Common.ush"
#include "SceneTextureParameters.ush"
#include "LightGridCommon.ush"
#include "DeferredLightingCommon.ush"

uint2 GroupSize;

RWBuffer<int> RWTileInfo;
[numthreads(THREADGROUP_SIZEX, 1, 1)]
void MainCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	if(DispatchThreadId.x > (GroupSize.x * GroupSize.y))
	{
		return;
	}

	const uint GridX = DispatchThreadId.x % GroupSize.x;
	const uint GridY = DispatchThreadId.x / GroupSize.x;
	const uint EyeIndex = 0;
	const FLightGridData GridData = GetLightGridData(EyeIndex);
	uint LocalNumLights = 0;
	LOOP
	for (int SliceIt = 0; SliceIt < GridData.CulledGridSize.z; SliceIt++)
	{
		const uint GridIndex = ComputeLightGridCellIndex(uint3(GridX, GridY, SliceIt), EyeIndex);
		const FCulledLightsGridData CulledLightsGrid = GetCulledLightsGrid(GridIndex, EyeIndex);
		
		LocalNumLights = CulledLightsGrid.NumLocalLights;

		if(LocalNumLights > 0)
		{
			break;
		}
	}

	if(LocalNumLights > 0)
	{
		RWTileInfo[2 * DispatchThreadId.x] = GridX;
		RWTileInfo[2 * DispatchThreadId.x + 1] = GridY;
	}
	else
	{
		RWTileInfo[2 * DispatchThreadId.x] = -1;
		RWTileInfo[2 * DispatchThreadId.x + 1] = -1;
	}
}

int LightGridPixelSize;
Buffer<int> TileInfo;
void MainVS(
	float2 TexCoord : ATTRIBUTE0,
	uint VertexId : SV_VertexID,
	uint InstanceId : SV_InstanceID,
	out float4 OutPosition : SV_POSITION)
{
	float YPos = TileInfo[2 * InstanceId + 1] + TexCoord.y;
	float XPos = TileInfo[2 * InstanceId] + TexCoord.x;

	float2 ScreenUV = float2(XPos, YPos) * LightGridPixelSize * View.BufferSizeAndInvSize.zw;
	float2 ScreenPosition = (ScreenUV.xy - View.ScreenPositionScaleBias.wz) / View.ScreenPositionScaleBias.xy;
	OutPosition = float4(ScreenPosition, 0, 1);

	if(TileInfo[2 * InstanceId] < 0)
	{
		OutPosition.xy = 0;
	}
}

void Main(float4 SvPosition : SV_POSITION, out float3 OutColorA : SV_Target0, out float4 OutColorB : SV_Target1)
{
	const uint2 PixelPos = SvPosition.xy;
	const float2 ScreenPosition = SvPositionToScreenPosition(SvPosition).xy;
	const uint EyeIndex = 0;

	float2 ScreenUV = ScreenPosition * View.ScreenPositionScaleBias.xy + View.ScreenPositionScaleBias.wz;
	float SceneDepth = CalcSceneDepth(ScreenUV);

	float3 TranslatedWorldPosition = mul(float4(ScreenPosition * SceneDepth, SceneDepth, 1), PrimaryView.ScreenToTranslatedWorld).xyz;

	uint GridIndex = ComputeLightGridCellIndex(uint2(PixelPos.x, PixelPos.y), SceneDepth);
	const FCulledLightsGridData CulledLightGridData = GetCulledLightsGrid(GridIndex, EyeIndex);

	const uint NumLocalLights = min(CulledLightGridData.NumLocalLights, GetNumLocalLights(EyeIndex));

	float3 Color = uint3(0.f, 0.f , 0.f);
	float3 SavedL = float3(0,0,0);
	float TotalWeight = 0;

	LOOP
	for (uint LocalLightListIndex = 0; LocalLightListIndex < NumLocalLights; LocalLightListIndex++)
	{
		const FLocalLightData LocalLight = GetLocalLightData(CulledLightGridData.DataStartIndex + LocalLightListIndex, EyeIndex);

		// extra-early out since we know light grid is sloppy and all lights in list are radial (have a range)
		// appears useless
		float InvLightRadiusSq = LocalLight.LightPositionAndInvRadius.w * LocalLight.LightPositionAndInvRadius.w;
		float DistLight = length2(TranslatedWorldPosition - LocalLight.LightPositionAndInvRadius.xyz) * InvLightRadiusSq;
		if (DistLight > 1.0f)
		{
			continue;
		}
		FDeferredLightData LightData = ConvertToDeferredLight_Mobile(LocalLight);

		float3 L = LightData.Direction;	// Already normalized
		float3 ToLight = L;
		float3 MaskedLightColor = LightData.Color;
		float LightMask = 1;
		if (LightData.bRadialLight)
		{
			LightMask = GetLocalLightAttenuation( TranslatedWorldPosition, LightData, ToLight, L );
			MaskedLightColor *= LightMask;
		}

		float WeightLight = 1 - DistLight;
		
		if( LightMask > 0)
		{
			TotalWeight += WeightLight;
			Color += MaskedLightColor;
			SavedL += L *  WeightLight;
		}
	}

	if (TotalWeight > 0)
	{
		SavedL = (SavedL / TotalWeight);
		OutColorA = float3(Color.x, Color.y, Color.z);
		OutColorB = 0.5f * float4(SavedL.x, SavedL.y, SavedL.z, 0)  + float4(0.5f, 0.5f, 0.5f, 0.5f);
	}
	else
	{
		OutColorA = float3(0, 0, 0);
		OutColorB = float4(0.5,0.5,0.5, 0.5);
	}
}
