// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#ifdef USE_STRATA_FORWARD_LIGHTING_COMMON

// Used by non-mobile path
half4 GetPrecomputedShadowFactors(FStrataPixelHeader StrataPixelHeader, float3 TranslatedWorldPosition)
{
	// PrecomputedShadowFactors are not supported on mobile at the moment
	float4 OutPrecomputedShadowFactors = StrataPixelHeader.HasZeroPrecShadowMask() ? 0.0f : 1.0f;
#if TRANSLUCENCY_LIGHTING_SURFACE_FORWARDSHADING && !SHADING_PATH_MOBILE
	OutPrecomputedShadowFactors.x = ComputeDirectionalLightStaticShadowing(TranslatedWorldPosition).x; // Is that actually correct for forward & deferred?
#else
	OutPrecomputedShadowFactors.x = 1;
#endif
	return half4(OutPrecomputedShadowFactors);
}

// Common function for forward lighting per pixel using light data
float3 StrataForwardLightingCommon(
	float Dither,
	FStrataIntegrationSettings Settings,
	FDeferredLightData LightData,
	float3 ToLight,
	float LightMask,
	float4 LightAttenuation,
	FRectTexture RectTexture,
	uint LightChannelMask,
	uint PrimitiveLightingChannelMask,
	half4 PrecomputedShadowFactors,
	float3 TranslatedWorldPosition,
	float SceneDepth,
	float3 BSDFColoredVisibility,
	FStrataPixelHeader StrataPixelHeader,
	FStrataBSDFContext StrataBSDFContext,
	inout FStrataEvaluateResult BSDFEvaluate)
{
	float3 Color = 0.0;

	if (LightMask > 0.0)
	{
		// Evaluate the ShadowTerm that can then be used when integrating the lighting
		FShadowTerms ShadowTerms = { StrataGetAO(StrataPixelHeader), 1.0, 1.0, InitHairTransmittanceData() };

		const uint FakeShadingModelID = 0;
		const float FakeContactShadowOpacity = 1.0f;
		GetShadowTerms(SceneDepth, PrecomputedShadowFactors, FakeShadingModelID, FakeContactShadowOpacity,
			LightData, TranslatedWorldPosition, ToLight, LightAttenuation, Dither, ShadowTerms);

		float Roughness = StrataGetBSDFRoughness(StrataBSDFContext.BSDF);
		FAreaLightIntegrateContext AreaLightContext = InitAreaLightIntegrateContext();
		BRANCH
		if (ShadowTerms.SurfaceShadow + ShadowTerms.TransmissionShadow > 0)
		{
			BSDFEvaluate = (FStrataEvaluateResult)0;

		#if NON_DIRECTIONAL_DIRECT_LIGHTING
			float Lighting;
			if (LightData.bRectLight)
			{
				FRect Rect = GetRect(ToLight, LightData);
				if (!IsRectVisible(Rect))
				{
					LightMask = 0.0f; // Rect light can be non visible due to barn door occlusion
				}
				AreaLightContext = CreateRectIntegrateContext(Roughness, StrataBSDFContext.N, StrataBSDFContext.V, Rect, RectTexture);
				Lighting = IntegrateLight(Rect);

				// We must have the evaluate inside the if due to the rectlight texture: it must be now be ambiguous which texture is going ot be used.
				// After te compilation, a local resource must map to a unique global resource (the default or the actual rect light texture).
				BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_AREA_LIGHT_RECT);
			}
			else
			{
				FCapsuleLight Capsule = GetCapsule(ToLight, LightData);
				AreaLightContext = CreateCapsuleIntegrateContext(Roughness, StrataBSDFContext.N, StrataBSDFContext.V, Capsule, LightData.bInverseSquared);
				Lighting = IntegrateLight(Capsule, LightData.bInverseSquared);

				BRANCH
				if(IsAreaLight(AreaLightContext.AreaLight))
				{
					BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_AREA_LIGHT_CAPSULE);
				}
				else
				{
					BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_PUNCTUAL_LIGHT);
				}
			}

			FLATTEN
			if (LightChannelMask & PrimitiveLightingChannelMask)
			{
				float3 DiffuseLuminance = Diffuse_Lambert(BSDFEvaluate.DiffuseColor) * Lighting;
				const float3 LightCommonMultiplier = LightData.Color * LightMask;
				Color += DiffuseLuminance * LightCommonMultiplier * BSDFColoredVisibility;
			}
		#else
			if (LightData.bRectLight)
			{
				FRect Rect = GetRect(ToLight, LightData);
				if (!IsRectVisible(Rect))
				{
					LightMask = 0.0f; // Rect light can be non visible due to barn door occlusion
				}
				AreaLightContext = CreateRectIntegrateContext(Roughness, StrataBSDFContext.N, StrataBSDFContext.V, Rect, RectTexture);

				// We must have the evaluate inside the if due to the rectlight texture: it must be now be ambiguous which texture is going ot be used.
				// After te compilation, a local resource must map to a unique global resource (the default or the actual rect light texture).
				BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_AREA_LIGHT_RECT);
			}
			else
			{
				FCapsuleLight Capsule = GetCapsule(ToLight, LightData);
				AreaLightContext = CreateCapsuleIntegrateContext(Roughness, StrataBSDFContext.N, StrataBSDFContext.V, Capsule, LightData.bInverseSquared);
				
				BRANCH
				if(IsAreaLight(AreaLightContext.AreaLight))
				{
					BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_AREA_LIGHT_CAPSULE);
				}
				else
				{
					BSDFEvaluate = StrataEvaluateBSDFCommon(StrataBSDFContext, ShadowTerms, AreaLightContext, Settings, INTEGRATION_PUNCTUAL_LIGHT);
				}
			}

			FLATTEN
			if (LightChannelMask & PrimitiveLightingChannelMask)
			{
				float3 DiffuseLuminance = BSDFEvaluate.IntegratedDiffuseValue;
				float3 SpecularLuminance = BSDFEvaluate.IntegratedSpecularValue * LightData.SpecularScale;
				const float3 LightCommonMultiplier = LightData.Color * LightMask;
				Color += (DiffuseLuminance + SpecularLuminance) * LightCommonMultiplier * BSDFColoredVisibility;
			}
		#endif
		}
	}

	return Color;
}

#endif // USE_STRATA_FORWARD_LIGHTING_COMMON



#ifdef USE_STRATA_ENV_LIGHTING_COMMON

// USE_DEFAULT_ENV_LIGHTING_INPUT is use to allow lighting input override
#ifndef USE_DEFAULT_ENV_LIGHTING_INPUT
#define USE_DEFAULT_ENV_LIGHTING_INPUT 1
#endif 

#if USE_DEFAULT_ENV_LIGHTING_INPUT
// Diffuse input lighting (sky)
float3 GetEnvDiffuseLighting(float3 InBentNormal)
{
	return GetSkySHDiffuse(InBentNormal) * View.SkyLightColor.rgb;
}

// Specular input lighting (reflection capture + sky)
float3 GetEnvSpecularLighting(
	float CompositeAlpha,
	float3 TranslatedWorldPosition,
	float3 SpecularDirection,
	float SpecularSafeRoughness,
	float IndirectIrradiance,
	float IndirectSpecularOcclusion,
	float3 ExtraIndirectSpecular,
	uint NumCulledReflectionCaptures,
	uint CaptureDataStartIndex)
{
	const bool bCompositeSkylight = true;
	return CompositeReflectionCapturesAndSkylightTWS(
		CompositeAlpha,
		TranslatedWorldPosition,
		SpecularDirection,
		SpecularSafeRoughness,
		IndirectIrradiance,
		IndirectSpecularOcclusion,
		ExtraIndirectSpecular,
		NumCulledReflectionCaptures,
		CaptureDataStartIndex,
		0,
		bCompositeSkylight);
}
#endif // USE_DEFAULT_ENV_LIGHTING_INPUT

void StrataEnvLightingCommon(
	in FStrataEnvLightResult StrataEnvLight,
	in FStrataPixelHeader StrataPixelHeader,
	in FStrataBSDFContext StrataBSDFContext,
	in FStrataBSDF BSDF,
	in float3 BentNormal,
	in float3 BSDFThroughput,
	in uint CaptureDataStartIndex,
	in uint NumCulledReflectionCaptures,
	in float ScreenAmbientOcclusion,
	in float CloudVolumetricAOShadow,
	in float TopLayerSpecularContributionFactor,
	in float3 TranslatedWorldPosition,
	in float CombinedScreenAndMaterialAO,
	inout float SSRReductionFactor,
	inout float3 DiffuseLighting,
	inout float3 SpecularLighting)
{
	FStrataIrradianceAndOcclusion StrataIrradianceAndOcclusion = StrataGetIrradianceAndAO(StrataPixelHeader);

	// Diffuse component
	DiffuseLighting = 0;
#if ENABLE_DYNAMIC_SKY_LIGHT
	const bool bProcessFrontFaceDiffuse = any(StrataEnvLight.DiffuseWeight > 0.0f);
	const bool bProcessBackFaceDiffuse = any(StrataEnvLight.DiffuseBackFaceWeight > 0.0f);
	if (bProcessFrontFaceDiffuse || bProcessBackFaceDiffuse)
	{
		// Compute the common sky visibility factors
		FSkyLightVisibilityData SkyVisData = GetSkyLightVisibilityData(StrataBSDFContext.N, StrataBSDFContext.N, StrataIrradianceAndOcclusion.MaterialAO, ScreenAmbientOcclusion, BentNormal);

		if (bProcessFrontFaceDiffuse)
		{
			// Finally sample the sky diffuse contribution (spherical harmonic, Lambert BRDF)
			float3 DiffuseLookup = GetEnvDiffuseLighting(BentNormal);
			// And accumulate
			// Note: Use diffuse directional albedo (i.e., DiffuseWeight) as first order approximation for env. integration (STRATA_TODO instead compute SH coefficients for Chan)
			DiffuseLighting = CloudVolumetricAOShadow * BSDFThroughput * (SkyVisData.SkyDiffuseLookUpMul * DiffuseLookup + SkyVisData.SkyDiffuseLookUpAdd) * StrataEnvLight.DiffuseWeight;
		}
		if (bProcessBackFaceDiffuse)
		{
			// We do not evaluate back face sky light visibility data because all the data we have is for the front face only.  This could be evaluated at some cost.
			// However, we do apply SkyVisData.SkyDiffuseLookUpMul for scaling consistency.

			// Finally sample the sky diffuse contribution (spherical harmonic, Lambert BRDF) along the opposite normal direction
			float3 DiffuseLookup = GetEnvDiffuseLighting(-BentNormal);
			// And accumulate
			// Note: Use diffuse directional albedo (i.e., DiffuseWeight) as first order approximation for env. integration (STRATA_TODO instead compute SH coefficients for Chan)
			DiffuseLighting += CloudVolumetricAOShadow * BSDFThroughput * (SkyVisData.SkyDiffuseLookUpMul * DiffuseLookup) * StrataEnvLight.DiffuseBackFaceWeight;
		}
	}
#endif // ENABLE_DYNAMIC_SKY_LIGHT

	// Specular component
	const bool bIsTopLayer = BSDF_GETISTOPLAYER(BSDF);
	SpecularLighting = 0;
#if STRATA_FASTPATH==0
	if (any((StrataEnvLight.SpecularWeight + StrataEnvLight.SpecularHazeWeight) > 0.0f))
#else
	if (any(StrataEnvLight.SpecularWeight > 0.0f))
#endif
	{
		float IndirectIrradiance = StrataIrradianceAndOcclusion.IndirectIrradiance;
	#if ENABLE_SKY_LIGHT && ALLOW_STATIC_LIGHTING
		BRANCH
		// Add in diffuse contribution from dynamic skylights so reflection captures will have something to mix with
		if (ReflectionStruct.SkyLightParameters.y > 0 && ReflectionStruct.SkyLightParameters.z > 0)
		{
			IndirectIrradiance += GetDynamicSkyIndirectIrradiance(BentNormal, StrataBSDFContext.N);
		}
	#endif

		// Compute some extra occlusion information from DFAO and sky light data
		float IndirectSpecularOcclusion = 1.0f;
		float3 ExtraIndirectSpecular = 0.0f;

	#if SUPPORT_DFAO_INDIRECT_OCCLUSION
		float IndirectDiffuseOcclusion;
		const bool bTwoSideFoliage = false;
		GetDistanceFieldAOSpecularOcclusion(BentNormal, StrataEnvLight.SpecularDirection, StrataEnvLight.SpecularSafeRoughness, bTwoSideFoliage, IndirectSpecularOcclusion, IndirectDiffuseOcclusion, ExtraIndirectSpecular);
		// Apply DFAO to IndirectIrradiance before mixing with indirect specular
		IndirectIrradiance *= IndirectDiffuseOcclusion;
	#endif

		float RoughnessSquared = StrataEnvLight.SpecularSafeRoughness * StrataEnvLight.SpecularSafeRoughness;
		float SpecularOcclusion = IndirectSpecularOcclusion * GetSpecularOcclusion(StrataBSDFContext.SatNoV, RoughnessSquared, CombinedScreenAndMaterialAO);

		SpecularLighting += BSDFThroughput * StrataEnvLight.SpecularWeight *
			GetEnvSpecularLighting(
				(bIsTopLayer ? TopLayerSpecularContributionFactor : 1.0f) * SpecularOcclusion,
				TranslatedWorldPosition,
				StrataEnvLight.SpecularDirection,
				StrataEnvLight.SpecularSafeRoughness,
				IndirectIrradiance,
				IndirectSpecularOcclusion,
				ExtraIndirectSpecular,
				NumCulledReflectionCaptures,
				CaptureDataStartIndex);

	#if STRATA_FASTPATH==0
		if (BSDF_GETHASHAZINESS(BSDF))
		{
			SpecularLighting += BSDFThroughput * StrataEnvLight.SpecularHazeWeight *
				GetEnvSpecularLighting(
					(bIsTopLayer ? TopLayerSpecularContributionFactor : 1.0f) * SpecularOcclusion,
					TranslatedWorldPosition,
					StrataEnvLight.SpecularHazeDirection,
					StrataEnvLight.SpecularHazeSafeRoughness,
					IndirectIrradiance,
					IndirectSpecularOcclusion,
					ExtraIndirectSpecular,
					NumCulledReflectionCaptures,
					CaptureDataStartIndex);

			// SSR is traced for the sharpest lob. The smoothest one does not rely on SSR so we need to lower energy coming from SSR according to the lobe blend weight.
			// And we also try to make the transition smooth using Haziness
			SSRReductionFactor -= bIsTopLayer ? dot(BSDFThroughput, float3(1.0f / 3.0f, 1.0f / 3.0f, 1.0f / 3.0f)) * StrataEnvLight.SSRReduction : 0.0f;
		}
	#endif 
	}
}

#endif // STRATA_ENV_LIGHTING_COMMON


