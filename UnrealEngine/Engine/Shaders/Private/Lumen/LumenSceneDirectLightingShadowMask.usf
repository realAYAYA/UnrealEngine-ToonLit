// Copyright Epic Games, Inc. All Rights Reserved.

#ifndef VIRTUAL_SHADOW_MAP
	#define VIRTUAL_SHADOW_MAP 0
#endif
#ifndef DENSE_SHADOW_MAP
	#define DENSE_SHADOW_MAP 0
#endif

#include "../Common.ush"
#include "LumenCardCommon.ush"
#include "LumenCardTile.ush"
#define SUPPORT_CONTACT_SHADOWS 0
#include "../DeferredLightingCommon.ush"
#include "../VolumeLightingCommon.ush"
#include "../ForwardShadowingCommon.ush"
#if VIRTUAL_SHADOW_MAP
	#include "../VirtualShadowMaps/VirtualShadowMapProjectionCommon.ush"
#endif
#include "SurfaceCache/LumenSurfaceCache.ush"
#if LIGHT_FUNCTION
	#include "/Engine/Generated/Material.ush"
	#include "../LightFunctionCommon.ush"
#endif
#include "LumenSceneDirectLightingPerLightShadowCommon.ush"
#if USE_CLOUD_TRANSMITTANCE
	#include "../VolumetricCloudCommon.ush"
#endif

RWStructuredBuffer<uint> RWShadowTraces;
RWStructuredBuffer<uint> RWShadowTraceAllocator;

float ShadowMapSamplingBias;
float VirtualShadowMapSamplingBias;
float HeightfieldShadowReceiverBias;

int VirtualShadowMapId;
uint UseIESProfile;

// Debug variables
uint ForceShadowMaps;

groupshared uint SharedNumShadowTraces;
groupshared uint SharedShadowTraces[THREADGROUP_SIZE_X * THREADGROUP_SIZE_Y];
groupshared uint SharedGlobalShadowTraceOffset;

[numthreads(THREADGROUP_SIZE_X, THREADGROUP_SIZE_Y, 1)]
void LumenSceneDirectLightingNonRayTracedShadowsCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint LinearGroupThreadId = GroupThreadId.x + GroupThreadId.y * THREADGROUP_SIZE_X;

#if THREADGROUP_SIZE_32
	uint LocalLightTileIndex = GroupId.x / 2;
	uint2 CoordInCardTile = GroupThreadId.xy + uint2(0, GroupId.x % 2 ? 0 : 4);
	uint ShadowMaskSize = SHADOW_MASK_CARD_TILE_DWORDS / 2;
	uint ShadowMaskOffset = GroupId.x % 2 ? 0 : 1;
#else
	uint LocalLightTileIndex = GroupId.x;
	uint2 CoordInCardTile = GroupThreadId.xy;
	uint ShadowMaskSize = SHADOW_MASK_CARD_TILE_DWORDS;
	uint ShadowMaskOffset = 0;
#endif

	if (LinearGroupThreadId < ShadowMaskSize)
	{
		SharedShadowMask[LinearGroupThreadId] = 0;
	}

	if (all(GroupThreadId.xy == 0))
	{
		SharedNumShadowTraces = 0;
		SharedGlobalShadowTraceOffset = 0;
		SharedShadowTraces[0] = 0;
	}

	GroupMemoryBarrierWithGroupSync();

	uint LightTileIndex = LocalLightTileIndex + LightTileOffsetsPerLight[LightIndex * NumViews + ViewIndex];
	FLightTileForShadowMaskPass LightTile = UnpackLightTileForShadowMaskPass(LightTiles[LightTileIndex]);
	uint2 TexelInCardPageCoord = LightTile.TileCoord * CARD_TILE_SIZE + CoordInCardTile;

	FLumenCardPageData CardPage = GetLumenCardPageData(LightTile.CardPageIndex + DummyZeroForFixingShaderCompilerBug);
	float2 AtlasUV = CardPage.PhysicalAtlasUVRect.xy + CardPage.PhysicalAtlasUVTexelScale * (TexelInCardPageCoord + 0.5f);
	float2 CardUV = CardPage.CardUVRect.xy + CardPage.CardUVTexelScale * (TexelInCardPageCoord + 0.5f);

	FShadowMaskRay ShadowMaskRay;
	ShadowMaskRay.ShadowFactor = 0.0f;
	ShadowMaskRay.bShadowFactorComplete = true;

	FLumenCardData Card = GetLumenCardData(CardPage.CardIndex);
	FLumenSurfaceCacheData SurfaceCacheData = GetSurfaceCacheData(Card, CardUV, AtlasUV);

	if (SurfaceCacheData.bValid)
	{
		FDeferredLightData LightData = LoadLightData(LIGHT_TYPE);
		float3 WorldNormal = SurfaceCacheData.WorldNormal;
		float3 WorldPosition = SurfaceCacheData.WorldPosition;
		float3 TranslatedWorldPosition = WorldPosition + LWCHackToFloat(PrimaryView.PreViewTranslation);

		float3 L = LightData.Direction;
		float3 ToLight = L;
		float CombinedAttenuation = 1.0f;
		float Attenuation = 1.0f;
		float LightMask = 1.0f;

#if LIGHT_TYPE != LIGHT_TYPE_DIRECTIONAL
		if (LightData.bRadialLight)
		{
			LightMask = GetLocalLightAttenuation(TranslatedWorldPosition, LightData, ToLight, L);
		}

		if (LightData.bRectLight)
		{
			FRect Rect = GetRect(ToLight, LightData);
			Attenuation = IntegrateLight(Rect);
		}
		else
		{
			FCapsuleLight Capsule = GetCapsule(ToLight, LightData);
			Capsule.DistBiasSqr = 0;
			Attenuation = IntegrateLight(Capsule, LightData.bInverseSquared);
		}

		CombinedAttenuation *= Attenuation * LightMask;
#endif

		CombinedAttenuation *= saturate(dot(WorldNormal, L));

		if (CombinedAttenuation > 0)
		{
			ShadowMaskRay.ShadowFactor = 1.0f;
			ShadowMaskRay.bShadowFactorComplete = false;
			bool bVSMValid = false;

			const float4 PostProjectionPosition = mul(float4(WorldPosition, 1.0), LWCHackToFloat(PrimaryView.WorldToClip));
			float ReceiverBias = 0.0f;
			if (Card.bHeightfield)
			{
				float3 WorldCameraOrigin = LWCHackToFloat(PrimaryView.WorldCameraOrigin);
				ReceiverBias = CalculateDistanceBasedHeightfieldBias(HeightfieldShadowReceiverBias, WorldPosition, WorldCameraOrigin);
			}

			// Shadow maps are culled so only query points inside the view frustum are valid
			if (all(and(PostProjectionPosition.xy < PostProjectionPosition.w, PostProjectionPosition.xy > -PostProjectionPosition.w)))
			{
				#if VIRTUAL_SHADOW_MAP
				{
					// Bias only ray start to maximize chances of hitting an allocated page
					FVirtualShadowMapSampleResult VirtualShadowMapSample = SampleVirtualShadowMapLWCHack(VirtualShadowMapId, WorldPosition, VirtualShadowMapSamplingBias + ReceiverBias, WorldNormal);

					bVSMValid = VirtualShadowMapSample.bValid;
					ShadowMaskRay.ShadowFactor *= VirtualShadowMapSample.ShadowFactor;
						
					// If there's also a dense shadow map present we need to sample both (unless the VSM determines we are fully shadowed anyways)
					ShadowMaskRay.bShadowFactorComplete = ShadowMaskRay.ShadowFactor < 0.01f;
					#if !DENSE_SHADOW_MAP
					ShadowMaskRay.bShadowFactorComplete = ShadowMaskRay.bShadowFactorComplete || VirtualShadowMapSample.bValid;
					#endif
				}
				#endif

				#if DENSE_SHADOW_MAP
				if (!ShadowMaskRay.bShadowFactorComplete)
				{
					float3 TranslatedWorldPositionForShadowing = GetCardWorldPositionForShadowing(TranslatedWorldPosition, L, WorldNormal, ShadowMapSamplingBias + ReceiverBias);

					#if LIGHT_TYPE == LIGHT_TYPE_DIRECTIONAL
					{
						#if DYNAMICALLY_SHADOWED
						{
							float SceneDepth = dot(TranslatedWorldPositionForShadowing - PrimaryView.TranslatedWorldCameraOrigin, View.ViewForward);

							bool bShadowingFromValidUVArea = false;
							float NewShadowFactor = ComputeDirectionalLightDynamicShadowing(TranslatedWorldPositionForShadowing, SceneDepth, bShadowingFromValidUVArea);

							if (bShadowingFromValidUVArea)
							{
								ShadowMaskRay.ShadowFactor *= NewShadowFactor;
								ShadowMaskRay.bShadowFactorComplete = VIRTUAL_SHADOW_MAP ? bVSMValid : true;
							}
						}
						#endif
					}
					#else
					{
						bool bShadowingFromValidUVArea = false;
						float NewShadowFactor = ComputeVolumeShadowing(TranslatedWorldPositionForShadowing, LightData.bRadialLight && !LightData.bSpotLight, LightData.bSpotLight, bShadowingFromValidUVArea);

						if (bShadowingFromValidUVArea) 
						{
							ShadowMaskRay.ShadowFactor *= NewShadowFactor;
							ShadowMaskRay.bShadowFactorComplete = VIRTUAL_SHADOW_MAP ? bVSMValid : true;
						}
					}
					#endif
				}
				#endif
			}

			#if LIGHT_FUNCTION
			if (ShadowMaskRay.ShadowFactor > 0.01f)
			{
				ShadowMaskRay.ShadowFactor *= GetLightFunction(TranslatedWorldPosition);
				ShadowMaskRay.bShadowFactorComplete = ShadowMaskRay.bShadowFactorComplete || ShadowMaskRay.ShadowFactor < 0.01f;
			}
			#endif

			#if USE_CLOUD_TRANSMITTANCE
			if (ShadowMaskRay.ShadowFactor > 0.01f)
			{
				float OutOpticalDepth = 0.0f;
				ShadowMaskRay.ShadowFactor *= lerp(1.0f, GetCloudVolumetricShadow(TranslatedWorldPosition, CloudShadowmapTranslatedWorldToLightClipMatrix, CloudShadowmapFarDepthKm, CloudShadowmapTexture, CloudShadowmapSampler, OutOpticalDepth), CloudShadowmapStrength);
				ShadowMaskRay.bShadowFactorComplete = ShadowMaskRay.bShadowFactorComplete || ShadowMaskRay.ShadowFactor < 0.01f;
			}
			#endif

			if (ShadowMaskRay.ShadowFactor > 0.01f && UseIESProfile > 0)
			{
				ShadowMaskRay.ShadowFactor *= ComputeLightProfileMultiplier(TranslatedWorldPosition, DeferredLightUniforms.TranslatedWorldPosition, -DeferredLightUniforms.Direction, DeferredLightUniforms.Tangent);
				ShadowMaskRay.bShadowFactorComplete = ShadowMaskRay.bShadowFactorComplete || ShadowMaskRay.ShadowFactor < 0.01f;
			}
		}
		else
		{
			ShadowMaskRay.ShadowFactor = 0.0f;
			ShadowMaskRay.bShadowFactorComplete = true;
		}
	}

	if (ForceShadowMaps != 0)
	{
		ShadowMaskRay.bShadowFactorComplete = true;
	}

	WriteSharedShadowMaskRay(ShadowMaskRay, GroupThreadId.xy, false);

	#if COMPACT_SHADOW_TRACES
	{
		if (!ShadowMaskRay.bShadowFactorComplete)
		{
			uint ShadowTraceIndex = 0;
			InterlockedAdd(SharedNumShadowTraces, 1, ShadowTraceIndex);

			FShadowTrace ShadowTrace;
			ShadowTrace.LightTileIndex = LightTileIndex;
			ShadowTrace.LightTileCoord = CoordInCardTile;

			SharedShadowTraces[ShadowTraceIndex] = PackShadowTrace(ShadowTrace);
		}
	}
	#endif

	GroupMemoryBarrierWithGroupSync();

	#if COMPACT_SHADOW_TRACES
	{
		if (all(GroupThreadId == 0))
		{
			InterlockedAdd(RWShadowTraceAllocator[0], SharedNumShadowTraces, SharedGlobalShadowTraceOffset);
		}
	}
	#endif

	GroupMemoryBarrierWithGroupSync();

	#if COMPACT_SHADOW_TRACES
	{
		if (LinearGroupThreadId < SharedNumShadowTraces)
		{
			RWShadowTraces[SharedGlobalShadowTraceOffset + LinearGroupThreadId] = SharedShadowTraces[LinearGroupThreadId];
		}
	}
	#endif

	if (LinearGroupThreadId < ShadowMaskSize)
	{
		RWShadowMaskTiles[SHADOW_MASK_CARD_TILE_DWORDS * LightTileIndex + ShadowMaskSize * ShadowMaskOffset + LinearGroupThreadId] = SharedShadowMask[LinearGroupThreadId];
	}
}
