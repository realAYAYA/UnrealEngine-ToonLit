// Copyright Epic Games, Inc. All Rights Reserved.

/**

Template hlsl for Data Channel DI functions to apply to their own that is shared accross all functions for the DI.

*/

//Offset of each parameter accessed by this script.
//Indices for accessing this table are embeded directly into the HLSL and we generate the table on the CPU accordingly.
//TODO: Can probably pack this into a single global buffer rather than one per DI.
Buffer<uint> {ParameterName}_ParamOffsetTable;

Buffer<float> {ParameterName}_DataFloat;
Buffer<int> {ParameterName}_DataInt32;
Buffer<float> {ParameterName}_DataHalf;

int {ParameterName}_ParameterOffsetTableIndex;
int {ParameterName}_Num;
int {ParameterName}_FloatStride;
int {ParameterName}_Int32Stride;
int {ParameterName}_HalfStride;

//Buffer containing back mapping from spawn particle exec index to the NDC index that spawned that particle.
//Arraged into power of two buckets so that we can avoid an entry per particle.
Buffer<int> {ParameterName}_NDCSpawnDataBuffer;

void Num_{ParameterName}(out int OutNum)
{
	OutNum = {ParameterName}_Num;
}

void GetNDCSpawnData_{ParameterName}(NiagaraEmitterID InEmitterID, int InExecIndex, out int OutNDCIndex, out int OutNDCSpawnIndex, out int OutNDCSpawnCount)
{
	/*
	Look up the various bits of spawn data from the NDC that spawned this particle.
	The first section of the NDCSpawnDataBuffer contains a SpawnCount for every NDC Item from which we can calcualte NDCSpawnCount.
	The next section is a series of buckets containing all NDC indices that contain enough particles to spawn 1 << Bucket particles.
	ie. if an NDC spawns 10 particles it will have an entry in the 8 and 2 buckets and none in any other bucket.
	From this we can map the main ExecIndex back into an NDCIndex that generated it. However ofc this mapping is by bucket and so particles from one NDC may not be contiuous in the spawn script execution/particle data.
	From the per NDCSpawnCount we can also determine if an NDC has an entry in each bucket and so calculate the SpawnIndex of each particle within the NDC Spawn.

	This is a little convoluted but means we can access the spawning NDC Index when spawning in a single large SpawnInfo/Execution.
	This power of 2 bucket system also means we can keep the indirection table size reasonable for large and small particle counts in exchange for some ALU work.

	|| NDCSpawnDataBuffer |
	|| Bucket Sizes	                                           || Spawn Counts for each NDC         || 2^15 Bucket   || 2^14 Bucket || ... || 2^1 Bucket || 2^0 Bucket   ||
	|| Bucket 0 Size | Bucket 1 Size | ... | Bucket 15 Size    || NDC 2 | ... | NDC N               || 0 | 0 | 5 | 7 || 0 |.........|| ... || 0 | 1 | 5  ||              || 
	
	Above shows the layout for NDCSpawnDataBuffer.
	All Emitters are packed into the same buffer. The first part of the buffer is a lookup table for each emitter (by emitter ID/Index) into the rest of the data.

	As we proceed through each ExecIndex in the spawn execution, we iterate through each bucket and can know for example, we can allocate the first 2^15 particles to NDC 0 as that has an entry in the 2^15 bucket.
	The 2^15 bucket is special in that it can have multiple entries for the same NDC as it's the final bucket. All other buckets have only one as higher particle counts just move the NDC into other buckets. 
	Less fills up lower buckets, more with fill up this and lower buckets. 2x will push it into the bucket above. Except in the final bucket if we hit 2x or more 2^15 then we just have to add another entry to that bucket.
	So in the above example the first and second 2^15 instances of the execution are given to NDC 0.
	The next 2^15 are given to 5, then 7. Now we've processed the 2^15 bucket, further instances take from the next bucket and so on until we're at the 2^0 bucket and we've handled all instances.
	*/

	OutNDCIndex = -1;
	OutNDCSpawnIndex = -1;
	OutNDCSpawnCount = -1;

	uint NumNDCEntries = {ParameterName}_Num;

	//First part of the buffer is the per emitter offsets into the data.
	//Grab the right base offset for this emitter.
	uint NDCSpawnDataOffset = { ParameterName }_NDCSpawnDataBuffer[InEmitterID.ID];
	if(NDCSpawnDataOffset == 0 || NumNDCEntries == 0)
	{
		OutNDCIndex = NDCSpawnDataOffset;
		OutNDCSpawnIndex = InEmitterID.ID;
		OutNDCSpawnCount = NumNDCEntries;

		return;
	}

	uint NDCSpawnData_BucketSizeStart = NDCSpawnDataOffset;
	uint NDCSpawnData_NDCCountsStart = NDCSpawnData_BucketSizeStart + 16;
	uint NDCSPawnData_NDCIndexBucketsStart = NDCSpawnData_NDCCountsStart + NumNDCEntries;
	
	uint MaxBucketExecIndex = 0;
	uint BucketEntryStart = NDCSPawnData_NDCIndexBucketsStart;

	[UNROLL]
	for(int BucketIdx = 0; BucketIdx < 16; ++BucketIdx)
	{
		uint BucketSize = (1U << 15U) >> BucketIdx;
		uint NumEntriesInBucket = {ParameterName}_NDCSpawnDataBuffer[NDCSpawnData_BucketSizeStart + BucketIdx];
		uint MinBucketExecIndex = MaxBucketExecIndex; 
		MaxBucketExecIndex += BucketSize * NumEntriesInBucket;
		
		if(InExecIndex < MaxBucketExecIndex)
		{
			//We found our bucket.
			//Now we need to find our NDCIndex Entry Index.
			uint NDCIndexEntry = (InExecIndex - MinBucketExecIndex) / BucketSize;			
			OutNDCIndex = {ParameterName}_NDCSpawnDataBuffer[BucketEntryStart + NDCIndexEntry];
			break;
		}
		
		BucketEntryStart += NumEntriesInBucket;
	}
	
	if(OutNDCIndex >= 0 && OutNDCIndex < (int)NumNDCEntries)
	{
		//Pull the NDC Spawn count from the first part of the buffer.
		OutNDCSpawnCount = {ParameterName}_NDCSpawnDataBuffer[NDCSpawnData_NDCCountsStart + OutNDCIndex];

		//Do another pass to calculate our SpawnIndex for this NDC within the total count for this NDC.
		OutNDCSpawnIndex = 0;
		MaxBucketExecIndex = 0;
		uint Count = OutNDCSpawnCount;
		[UNROLL]
		for(int BucketIdx = 0; BucketIdx < 16; ++BucketIdx)
		{
			uint BucketSize = (1U << 15U) >> BucketIdx;
			uint Mask = (0xFFFFU >> (BucketIdx + 1U));
			uint CountMasked = Count & ~Mask;
			Count &= Mask;
			uint NumNDCEntriesInBucket = CountMasked >> (15U - BucketIdx);
			uint NumEntriesInBucket = {ParameterName}_NDCSpawnDataBuffer[NDCSpawnData_BucketSizeStart + BucketIdx];
			uint NumNDCInstancesInBucket = NumNDCEntriesInBucket * BucketSize;

			uint MinBucketExecIndex = MaxBucketExecIndex;
			MaxBucketExecIndex += BucketSize * NumEntriesInBucket;
			if (InExecIndex < MaxBucketExecIndex && NumNDCInstancesInBucket > 0)
			{
				uint NDCIndexEntry = (InExecIndex - MinBucketExecIndex) >> (15U - BucketIdx);
				uint MinNDCBucketExecIndex = MinBucketExecIndex + (BucketSize * NDCIndexEntry);

				OutNDCSpawnIndex += (InExecIndex - MinNDCBucketExecIndex);
				break;
			}
			else
			{
				OutNDCSpawnIndex += NumNDCInstancesInBucket;
			}
		}
	}
	else
	{
		OutNDCIndex = -1;
		OutNDCSpawnIndex = -1;
		OutNDCSpawnCount = -1;
	}
}

// Outputs the starting offset of each component in the channel data for a partiuclar parameter.
bool GetParameterLayoutInfo_{ParameterName}(uint ParameterIndex, out uint OutFloatRegister, out uint OutInt32Register, out uint OutHalfRegister)
{
	int ParameterTableStart = {ParameterName}_ParameterOffsetTableIndex;
	if(ParameterTableStart != -1)
	{
		const int NumComponents = 3;
		uint ParamLayoutStart = ParameterTableStart + NumComponents * ParameterIndex;
		OutFloatRegister = {ParameterName}_ParamOffsetTable[ParamLayoutStart];
		OutInt32Register = {ParameterName}_ParamOffsetTable[ParamLayoutStart + 1];
		OutHalfRegister = {ParameterName}_ParamOffsetTable[ParamLayoutStart + 2];
		return true;
	}
	else
	{
		OutFloatRegister = -1;
		OutInt32Register = -1;
		OutHalfRegister = -1;
		return false;
	}
}

